Transit.format_time = (input) ->
  date = if input not instanceof Date then new Date(input) else input
  hours = date.getHours() % 12 || 12
  minutes = date.getMinutes()
  padded_minutes = "#{if minutes < 10 then '0' else ''}#{minutes}"
  period = if date.getHours() < 12 then 'am' else 'pm'
  "#{hours}:#{padded_minutes} #{period}"

Transit.format_otp_date = (input) ->
  date = if input not instanceof Date then new Date(input) else input
  month = date.getMonth() + 1
  day = date.getDate()
  year = date.getFullYear()
  "#{month}-#{day}-#{year}"

Transit.format_otp_time = (input) ->
  date = if input not instanceof Date then new Date(input) else input
  hours = date.getHours()
  minutes = date.getMinutes()
  seconds = date.getSeconds()
  padded_minutes = "#{if minutes < 10 then '0' else ''}#{minutes}"
  padded_seconds = "#{if seconds < 10 then '0' else ''}#{seconds}"
  "#{hours}:#{padded_minutes}:#{padded_seconds}"

Transit.format_duration = (seconds, minimize=false) ->
  return "NOW!" if seconds < 60 and not minimize
  mins = Math.floor(seconds / 60)
  if mins > 59
    hrs = Math.floor(mins / 60)
    mins = mins % 60
  else hrs = 0

  if hrs > 0 then "#{hrs}#{if minimize then 'h' else ' hours'}, #{mins}#{if minimize then 'm' else ' minutes'}"
  else "#{mins}#{if minimize then 'm' else ' minutes'}"

Transit.format_deviation = (seconds) ->
  minutes = Math.round(seconds / 60)
  if minutes > 0
    "#{minutes} minute#{if minutes > 1 then 's' else ''} early"
  else if minutes < 0
    "#{Math.abs(minutes)} minute#{if minutes < -1 then 's' else ''} late"
  else if minutes == 0 
    'on time'
  else null

Transit.deviation_class = (seconds) ->
  if seconds > 30 then 'early label-success'
  else if seconds > -30 then 'on-time label-info'
  else 'late label-important'

Transit.pad = (num, zeroes) -> (1e10+num+"").slice(-zeroes)

Transit.storage_get = (key) ->
  JSON.parse(localStorage.getItem(key) ? '{}')

Transit.storage_set = (key, value) ->
  localStorage.setItem(key, JSON.stringify(value))

Transit.storage_clear = (key) ->
  localStorage.setItem(key, undefined)

Transit.setTitle = (title) =>
  $("#title h3").text(title)

Transit.setTitleHTML = (contents...) =>
  title = $("#title h3").html('')
  title.append(content) for content in contents

Transit.errorMessage = (title, message) =>
  if title?
    @$('.alert').html(message).prepend($("<strong>").html(title)).show()
  else
    @$('.alert').html('').hide()

Transit.errorPage = (heading, title, message) =>
  $(@el).html(@template(heading: heading, title: title, message: message))

# toggles appearance of the Settings menu via the gear button
Transit.toggleMenu = () =>
  $('#settings-bg').toggle()
  $('#settings-menu').toggle()
  $('#settings-button').toggleClass('active')

Transit.Markers =
  Start: L.Icon.extend
    iconSize: new L.Point(33, 35)
    iconAnchor: new L.Point(11, 35)
    iconUrl: '<%= image_path("icons/icon-start-location.png") %>'
    shadowUrl: null
  End: L.Icon.extend
    iconSize: new L.Point(33, 35)
    iconAnchor: new L.Point(11, 35)
    iconUrl: '<%= image_path("icons/icon-end-location.png") %>'
    shadowUrl: null
  Stop: L.Icon.extend
    iconSize: new L.Point(20, 20)
    iconAnchor: new L.Point(10, 20)
    iconUrl: '<%= image_path("icons/icon-busstop.png") %>'
    shadowUrl: null
  StopDot: L.Icon.extend
    iconSize: new L.Point(10, 10)
    iconAnchor: new L.Point(5, 5)
    iconUrl: '<%= image_path("icons/icon-stopdot.png") %>'
    shadowUrl: null

window.HTML = {}
# HTML GENERATORS (jQuery wrappers)
HTML.tag = (tagname, classes, body...) ->
  html = $(tagname).addClass(classes)
  html.append text for text in body
  html
HTML.div = (classes, body...) ->
  HTML.tag "<div>", classes, body...
HTML.span = (classes, body...) ->
  HTML.tag "<span>", classes, body...
HTML.li = (classes, body...) ->
  HTML.tag "<li>", classes, body...
HTML.link = (href, classes, body...) ->
  HTML.tag("<a>", classes, body...).attr("href", href)
HTML.btn = (classes, text, href=null) ->
  if href?
    HTML.link href, "btn #{classes}", text
  else
    HTML.span "btn #{classes}", text
# str() function returns outer HTML of tag and children as string
# and enables the HTMLpers to be used in javascript templates through
# eco's raw output tag
$.fn.str = () -> @prop('outerHTML')